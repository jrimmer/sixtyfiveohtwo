2065 

8912 ,S6,D1"
20 HOME:PRINT"If you are on a hard drive press Return.If you are not, then put the disk with  the file 'Config' in "F$" and":PRINT"press Return.":GETA$
30 SN=26:HOME
40 WN=100:AN=100
50 ONERRGOTO120
60 D$=CHR$(13)+CHR$(4):DIMS(50),W$(WN),A$(AN),T$(50),V(50),M(50),D(50),P(50),R(50),B(50),T(50),PA$(50)
70 DIMC$(20),B$(20)
80 PRINTD$"OPENCONFIG"F$:PRINTD$"READCONFIG":FORX=1TO20:INPUTI$,V,S,D:C$(X)=",V"+STR$(V)+",S"+STR$(S)+",D"+STR$(D):NEXT:PRINTD$"CLOSE"
90 PRINTD$"OPENWEAPONS,L40"C$(19):FORX=1TOWN:PRINTD$"READWEAPONS,R"X:INPUTW$(X),Y,Y:NEXT:PRINTD$"CLOSE"
100 PRINTD$"OPENARMOR,L40"C$(1):FORX=1TOAN:PRINTD$"READARMOR,R"X:INPUTA$(X),Y,Y:NEXT:PRINTD$"CLOSE"
110 GOTO760
120 CALL-3288:GOTO760
130 PRINT"[A] "NA$"  Pass: "PA$
140 PRINT"[1] Calls Per Day: "+STR$(CP)
150 PRINT"[2] Calls Today: "+STR$(CA)
160 PRINT"[3] Total Calls: "+STR$(CL)
170 PRINT"[4] Adventure Access: "+AD$
180 PRINT
190 PRINT"[5] Strength: "+STR$(ST)
200 PRINT"[7] Agility: "+STR$(AG)
210 PRINT"[8] Intelligence: "+STR$(CH)
220 PRINT"[9] Wisdom: "+STR$(WI)
230 PRINT"[10] Gold: "+STR$(GD)
240 PRINT"[11] Gold in The Bank: "BA
250 PRINT"[12] Food: "+STR$(FD)
260 PRINT
270 PRINT"[13] Level #"LE" "L$(1)
280 PRINT"[14] Maximum Hit Points: "+STR$(TH)
290 PRINT"[15] Maximum Power: "+STR$(TP)
300 PRINT"[16] Power: "+STR$(PO)
310 PRINT"[17] Weapon: "+W$(WE)
320 PRINT"[18] Armor: "+A$(AR)
330 PRINT"[19] Fight: "+FI$
340 PRINT:PRINT"Which(20=Save)-> ";:GOSUB640:PRINT
350 IFI$="Q"THEN760
360 IFI$="A"THENPRINT"Name->";:GOSUB640:NA$=I$:GOTO130
370 IFI$="B"THENPRINT"Password->";:GOSUB640:PA$=I$:GOTO130
380 X=VAL(I$):IFX<1ORX>20THEN130
390 IFX=1THENPRINT"Calls Per Day-> ";:GOSUB640:CP=VAL(I$):GOTO130
400 IFX=2THENPRINT"Calls Today->";:GOSUB640:CA=VAL(I$):GOTO130
410 IFX=3THENPRINT"Total Calls->";:GOSUB640:CL=VAL(I$):GOTO130
420 IFX=4THENPRINT"Adventure Access->";:GOSUB640:AD$=I$:GOTO130
430 IFX=5THENPRINT"Strength->";:GOSUB640:ST=VAL(I$):GOTO130
440 IFX=6THENPRINT"Dexterity->";:GOSUB640:DE=VAL(I$):GOTO130
450 IFX=7THENPRINT"Agility->";:GOSUB640:AG=VAL(I$):GOTO130
460 IFX=8THENPRINT"Intelligence->";:GOSUB640:CH=VAL(I$):GOTO130
470 IFX=9THENPRINT"Wisdom->";:GOSUB640:WI=VAL(I$):GOTO130
480 IFX=10THENPRINT"Gold->";:GOSUB640:GD=VAL(I$):GOTO130
490 IFX=12THENPRINT"Food->";:GOSUB640:FD=VAL(I$):GOTO130
500 IFX=13THENPRINT"Level->";:GOSUB640:LE=VAL(I$):GOSUB1150:GOTO130
510 IFX=14THENPRINT"Maximum Hit Points->";:GOSUB640:TH=VAL(I$):GOTO130
520 IFX=11THENPRINT"Gold in the Bank->";:GOSUB640:BA=VAL(I$):GOTO130
530 IFX=15THENPRINT"Maximum Power->";:GOSUB640:TP=VAL(I$):GOTO130
540 IFX=16THENPRINT"Power->";:GOSUB640:PO=VAL(I$):GOTO130
550 IFX=17THENPRINT"Weapon->";:GOTO600
560 IFX=18THENPRINT"Armor->";:GOTO620
570 IFX=20THENHOME:GOSUB720:GOTO130
580 IFX=19THENPRINT"Fight->";:GOSUB640:FI$=I$:GOTO130
590 GOTO130
600 GOSUB640:X=VAL(I$):IFX<1ORX>WNTHENX=17:GOTO550
610 WE=X:GOTO130
620 GOSUB640:X=VAL(I$):IFX<1ORX>ANTHENX=18:GOTO560
630 AR=X:GOTO130
640 INPUT"";I$:RETURN
650 PRINTD$"OPENSTATS,L200"C$(15)
660 PRINTD$"READSTATS,R"A
670 INPUTNA$,FI$,LE,AG,WE,AR,EX,GD,ST,TH,HP,PA$,WI,CH,LS,CA,CL,AD$,CP,FD,PO,TP,BA,PA,DE,DA$
680 FORX=1TOSN:INPUTS(X):NEXT
690 PRINTD$"CLOSE"
700 GOSUB1150
710 RETURN
720 PRINTD$"OPENSTATS,L200"C$(15):PRINTD$"WRITESTATS,R"A:PRINTNA$:PRINTFI$:PRINTLE:PRINTAG:PRINTWE:PRINTAR:PRINTEX:PRINTGD:PRINTST:PRINTTH:PRINTHP:PRINTPA$:PRINTWI:PRINTCH:PRINTLS:PRINTCAL:PRINTCL:PRINTAD$:PRINTCP
730 PRINTFD:PRINTPO:PRINTTP:PRINTBA:PRINTPA:PRINTDE:PRINTDA$
740 FORX=1TOSN:PRINTS(X):NEXT
750 PRINTD$"CLOSE":RETURN
760 HOME:PRINT"Proving Grounds Editor by The Time Lord":PRINT:PRINT"(C) Mike Heinstein 1985":PRINT:PRINT"[1] Edit User"
770 PRINT"[2] Delete User"
780 PRINT"[3] Validate User"
790 PRINT"[4] Rob all Treasuries"
800 PRINT"[5] Turn All the Fights On"
810 PRINT"[6] Re-Roll the Entire Board"
820 PRINT"[7] Run Board"
830 PRINT"[8] Board Module Editor"
840 PRINT"[9] Change/Edit Voting Topic"
850 PRINT"[10] Search for a User"
860 PRINT"[11] Count/List Validated Users"
870 PRINT"[12] List Users"
880 PRINT"[13] Dos Command"
890 PRINT"[14] Change Date(Only If No Clock)"
900 PRINT"[15] Re-Do Ladder and Rank"
910 PRINT:PRINT"Which-> ";:GOSUB640
920 PRINT
930 ONERRGOTO760
940 CF$="":CB$="":CH$="":CO$=""
950 IFI$<>"15"THEN980
960 HOME:PRINT:PRINT:PRINT"Re-Do Ladder and Rank?  If so, it will  put up the board after....Y/N? ->: ";:INPUT"";A$:IFA$="Y"THENPRINT:PRINTD$"RUNRESET1"C$(4)
970 IFI$="N"THEN760
980 IFI$="14"THEN2860
990 IFI$="9"THEN2320
1000 IFI$="13"THEN2810
1010 IFI$="12"THENCO$="Y":GOTO1220
1020 IFI$="11"THENNU=0:CH$="Y":GOTO1220
1030 IFI$="10"THEN2690
1040 IFI$="1"THENPRINT"Enter User #->";:GOSUB1140:HOME:A=VAL(I$):GOSUB650:GOTO130
1050 IFI$="2"THENPRINT"Enter User #->";:GOSUB1140:A=VAL(I$):GOSUB650:PRINT:PRINT"Delete "NA$"? ";:GOSUB1140:IFI$="Y"THENNA$="Empty":GOSUB720:GOTO760
1060 IFI$="3"THENPRINT"Enter User #->";:GOSUB1140:A=VAL(I$):GOSUB650:PRINT:PRINT"Validate "NA$"? ";:GOSUB1140:IFI$="Y"THENCP=2:AD$="Yes":GOSUB720:GOTO760
1070 CF$="":CB$="":RE$=""
1080 IFI$="4"THENPRINT"Rob all Treasuries? ";:GOSUB1140:IFI$="Y"THENCB$="Y":GOTO1220
1090 IFI$="5"THENPRINT"Turn all the Fights On? ";:GOSUB1140:IFI$="Y"THENCF$="Y":GOTO1220
1100 IFI$="6"THENPRINT"Re-Roll the Board? ";:GOSUB1140:IFI$="Y"THENRE$="Y":GOTO1220
1110 IFI$="7"THENPRINTD$"RUNSTART"C$(14)
1120 IFI$="8"THEN1600
1130 GOTO760
1140 GOTO640
1150 PRINTD$"OPENLEVELS"C$(7):PRINTD$"READLEVELS":IFLE=1THENINPUTL$(1),A(1),L$(2),A(2):L$(0)="":A(0)=0:GOTO1190
1160 IFLE=2THEN1180
1170 FORX=1TO(LE-2):INPUTL$(1),A(1):NEXT
1180 INPUTL$(0),A(0),L$(1),A(1),L$(2),A(2)
1190 PRINTD$"CLOSE"
1200 EX=A(1):GOTO1210
1210 RETURN
1220 PRINTD$"OPENDATA,D1"C$(5)
1230 PRINTD$"READDATA"
1240 INPUTA,A,DA$,DA$
1250 PRINTD$"CLOSE"
1260 PRINTD$"OPENSTATS,L200"C$(15)
1270 FORX=1TOA
1280 POKE216,0
1290 PRINTD$"READSTATS,R"X
1300 IFCH$="Y"ORCO$="Y"THEN1330
1310 HOME:PRINTX" out of "A
1320 ONERRGOTO1360
1330 INPUTNA$,FI$,LE,AG,WE,AR,EX,GD,ST,TH,HP,PA$,WI,CH,LS,CA,CL,AD$,CP,FD,PO,TP,BA,PA,DE,I$
1340 FORY=1TOSN:INPUTS(Y):NEXT
1350 GOTO1370
1360 CALL-3288
1370 IFCH$="Y"ANDNA$<>"EMPTY"ANDAD$="Yes"THENNU=NU+1:PRINTNU"] "NA$" #"X:GOTO1550
1380 IFCH$="Y"THEN1550
1390 IFCO$="Y"THENPRINTX"] "NA$:GOTO1550
1400 IFRE$<>"Y"THEN1470
1410 ST=10:AG=10:WI=10:CH=10:DE=0:WE=2:AR=2:EX=0:GD=500:LE=1:FI$="On":FD=100
1420 HP=20:TH=20
1430 CA=0
1440 PO=20:TP=20:BA=100:PA=-1
1450 FORY=1TO26:S(Y)=0:NEXT
1460 S(3)=1:S(6)=1:S(25)=2:S(20)=1:S(2)=2:S(10)=1
1470 IFNA$="EMPTY"THEN1550
1480 IFCF$="Y"ANDFI$="On"THEN1550
1490 IFCF$="Y"THENFI$="On"
1500 IFCB$="Y"ANDBA<1THEN1550
1510 IFCB$="Y"THENBA=.999
1520 PRINTD$"WRITESTATS,R"X:PRINTNA$:PRINTFI$:PRINTLE:PRINTAG:PRINTWE:PRINTAR:PRINTEX:PRINTGD:PRINTST:PRINTTH:PRINTHP:PRINTPA$:PRINTWI:PRINTCH:PRINTLS:PRINTCA:PRINTCL:PRINTAD$:PRINTCP
1530 PRINTFD:PRINTPO:PRINTTP:PRINTBA:PRINTPA:PRINTDE:PRINTI$
1540 FORY=1TOSN:PRINTS(Y):NEXT
1550 NEXT
1560 PRINTD$"CLOSE"
1570 IFCH$="Y"THENPRINT:PRINTNU" Validated Users.":INPUT"";A$
1580 IFCO$="Y"THENPRINT:INPUT"";A$
1590 GOTO760
1600 PRINTD$"OPENBOARDS,L55"C$(3)
1610 X=0
1620 ONERRGOTO1670
1630 X=X+1
1640 PRINTD$"READBOARDS,R"X
1650 INPUTT$(X),V(X),M(X),S(X),D(X),P(X),R(X),B(X),T(X),PA$(X)
1660 GOTO1630
1670 CALL-3288:X=X-1:PRINTD$"CLOSE":BN=X
1680 AZ=BN
1690 HOME:PRINT"Board Editor"
1700 PRINT:PRINTBN" boards active"
1710 PRINT
1720 FORX=1TOBN:PRINT"["X"] "+T$(X)
1730 NEXT
1740 PRINT
1750 PRINT"[A] Create New Board"
1760 PRINT"[B] Delete Board"
1770 PRINT"[ ] Type number of board to edit"
1780 PRINT
1790 INPUT"Edit Which->";I$
1800 X=VAL(I$)
1810 IFI$="A"THEN2160
1820 IFI$="B"THEN2230
1830 IFI$="Q"THEN760
1840 IFX<1ORX>BNTHEN1690
1850 EN=X
1860 HOME
1870 PRINT"Edit Board":PRINT
1880 PRINT"[1] Name: "T$(EN)
1890 PRINT"[2] Maximum Messages: "M(EN)
1900 PRINT"[3] Slot: "S(EN)
1910 PRINT"[4] Drive: "D(EN)
1920 PRINT"[5] Volume: "V(EN)
1930 X=T(EN):GOSUB3240:PRINT"[6] Message posted under: "O$
1940 X=P(EN):GOSUB3050:PRINT"[7] Who can post: "O$
1950 X=R(EN):GOSUB3090:PRINT"[8] Who can read: "O$
1960 PRINT"[9] Board Master is user #"B(EN)
1965 IFP(EN)=4THENPRINT"[10] Password: "PA$(EN)
1970 PRINT"[S] Save"
1980 PRINT:INPUT"Which->";I$:X=VAL(I$)
1990 IFI$="Q"THEN1690
2000 IFI$="S"THEN2040
2010 IFX<1ORX>10THEN1860
2020 ONXGOSUB3190,3200,3210,3230,3220,3280,3140,3160,3180,3300
2030 GOTO1860
2040 PRINTD$"OPENBOARDS,L55"C$(3):PRINTD$"DELETEBOARDS":PRINTD$"OPENBOARDS,L55"
2050 FORX=1TOBN
2060 PRINTD$"WRITEBOARDS,R"X
2070 PRINTT$(X):PRINTV(X):PRINTM(X):PRINTS(X):PRINTD(X):PRINTP(X):PRINTR(X):PRINTB(X):PRINTT(X):PRINTPA$(X)
2080 NEXT
2090 PRINTD$"CLOSE"
2100 Y=0
2110 FORX=1TOBN:IFM(X)>YTHENY=M(X):NEXT
2120 PRINTD$"OPENBDATA"C$(2):PRINTD$"WRITEBDATA":PRINTBN:PRINTY:PRINTD$"CLOSE"
2130 PRINT
2140 IFAZ<>BNTHENPRINTD$"OPENQSCAN"C$(10):PRINTD$"DELETEQSCAN"
2150 GOTO1690
2160 HOME
2170 EN=BN+1
2180 GOSUB3190:GOSUB3220:GOSUB3210:GOSUB3230:GOSUB3200:GOSUB3280:GOSUB3140:GOSUB3160:GOSUB3180:IFP(EN)=4THENGOSUB3300
2190 PRINT:INPUT"Ok? Y/N";A$:IFA$<>"Y"THEN1690
2200 BN=BN+1
2210 GOTO2040
2220 INPUT"";I$:RETURN
2230 PRINT:PRINT:INPUT"Delete What Board->";I$:X=VAL(I$)
2240 IFX<1ORX>BNTHEN1690
2250 PRINT:PRINT"Delete The "T$(X)+" Board";:INPUTI$:IFI$<>"Y"THEN1690
2260 Y=X
2270 FORX=(Y+1)TOBN
2280 T$(X-1)=T$(X):V(X-1)=V(X):M(X-1)=M(X):S(X-1)=M(X):S(X-1)=S(X):D(X-1)=D(X):P(X-1)=P(X):R(X-1)=R(X):B(X-1)=B(X):T(X-1)=T(X)
2290 NEXT
2300 BN=BN-1
2310 GOTO2040
2320 HOME:PRINT"Write Voting Topic"
2330 PRINT:PRINT"[A] Write New Topic"
2340 PRINT"[B] Save Topic"
2350 PRINT"[C] List Topic"
2360 PRINT"[D] Edit Topic"
2370 PRINT:INPUT"Which(Q=Quit)->";I$
2380 IFI$="Q"THEN760
2390 IFI$="C"THEN2590
2400 IFI$="D"THEN2640
2410 IFI$="A"THEN2500
2420 IFI$="B"THENPRINT:INPUT"Save Voting Topic?";I$:IFI$<>"Y"THEN2320
2430 PRINTD$"OPENVOTING"C$(18):PRINTD$"DELETEVOTING":PRINTD$"OPENVOTING"
2440 PRINTD$"WRITEVOTING"
2450 FORX=1TOVN
2460 PRINTV$(X);",0"
2470 NEXT
2480 PRINTD$"CLOSE"
2490 GOTO2320
2500 PRINT
2510 PRINT"What is the Topic?  Do not press Return until you are complete done.":PRINT:INPUT"";T$:PRINTD$"OPENVOTING.TOP"C$(17):PRINTD$"DELETEVOTING.TOP":PRINTD$"OPENVOTING.TOP":PRINTD$"WRITEVOTING.TOP":PRINTT$:PRINT:PRINTD$"CLOSE
2520 PRINT:PRINT"Input choices 1-9.  type 'Q' on it's own line  to stop adding choices.  I.E. if you only want 4 choices type 'Q' on     line 5.":PRINT
2530 FORX=1TO9
2540 PRINTX"] ";:INPUT"";V$(X)
2550 IFV$(X)="Q"THENX=X-1:VN=X:GOTO2320
2560 NEXTX
2570 VN=X-1
2580 GOTO2320
2590 PRINT
2600 FORX=1TOVN
2610 PRINTX"] "V$(X)
2620 NEXT
2630 PRINT:INPUT"";A$:GOTO2320
2640 PRINT:INPUT"Input Line To Edit->";I$:IFI$="Q"THEN2320
2650 X=VAL(I$):IFX<1ORX>VNTHEN2640
2660 PRINT:PRINTX"] "V$(X):PRINT
2670 PRINTX"] ";:INPUT"";V$(X)
2680 GOTO2320
2690 PRINT:INPUT"What is the Users Name->";I$
2700 X=0
2710 ONERRGOTO2790
2720 PRINTD$"OPENSTATS,L200"C$(15)
2730 X=X+1
2740 PRINTD$"READSTATS,R"X
2750 HOME:PRINTX
2760 INPUTA$
2770 IFA$=I$THEN2800
2780 GOTO2730
2790 CALL-3288:PRINTD$"CLOSE":PRINT"There Is No Such User.":INPUT"";A$:GOTO760
2800 PRINTD$"CLOSE":PRINTI$" is user "X:INPUT"";A$:GOTO760
2810 PRINT:INPUT"Dos(Q=Quit)->";A$
2820 IFA$="Q"THEN760
2830 ONERRGOTO2850
2840 PRINTD$""A$:PRINT:INPUT"";A$:GOTO760
2850 CALL-3288:PRINT:PRINT"Dos Error!":PRINT:INPUT"";A$:GOTO760
2860 PRINTD$"OPENDATA"C$(5):PRINTD$"READDATA":FORX=1TO10:INPUTZ$(X):NEXT:PRINTD$"CLOSE"
2870 HOME:PRINT:PRINT"Current Date-> "Z$(4):PRINT:PRINT"Change it? Y/N: ";:INPUT"";A$:IFA$<>"Y"ANDA$<>"N"THEN2870
2880 IFA$="N"THEN760
2890 B$(1)="SUN":B$(2)="MON":B$(3)="TUE":B$(4)="WED":B$(5)="THU":B$(6)="FRI":B$(7)="SAT"
2900 HOME
2910 PRINT"Input Day:":PRINT:PRINT:FORX=1TO7:PRINTX"] "B$(X):NEXT
2920 PRINT:PRINT"-> ";:INPUT"";A$:X=VAL(A$):IFX<1ORX>7THEN2900
2930 C$=B$(X)+" "
2940 B$(1)="JAN":B$(2)="FEB":B$(3)="MAR":B$(4)="APR":B$(5)="MAY":B$(6)="JUN":B$(7)="JUL":B$(8)="AUG":B$(9)="SEP":B$(10)="NOV":B$(11)="OCT":B$(12)="DEC"
2950 HOME:PRINT"Input Month":PRINT:PRINT:FORX=1TO12:PRINTX"] "B$(X):NEXT
2960 PRINT:PRINT"-> ";:INPUT"";A$:X=VAL(A$):IFX<1ORX>12THEN2950
2970 C$=C$+B$(X)+" "
2980 HOME:PRINT"Input Day(1-31)-> ";:INPUT"";A$:X=VAL(A$):IFX<1ORX>31THEN2980
2990 IFX>9THENC$=C$+A$:GOTO3010
3000 C$=C$+" "+A$
3010 PRINT:PRINT"Date-> "C$:PRINT:PRINT"Correct? Y/N-> ";:GETA$:IFA$<>"Y"ANDA$<>"N"THEN3010
3020 IFA$="N"THEN2880
3030 Z$(4)=C$:PRINTD$"OPENDATA":PRINTD$"WRITEDATA":FORX=1TO10:PRINTZ$(X):NEXT:PRINTD$"CLOSE"
3040 GOTO760
3050 IFX=1THENO$="Sysop Only"
3060 IFX=2THENO$="Validated Users Only"
3070 IFX=3THENO$="All Users"
3075 IFX=4THENO$="Password Needed"
3080 RETURN
3090 IFX=1THENO$="Validated users Only"
3100 IFX=2THENO$="All Users"
3110 RETURN
3120 PRINT
3130 PRINT
3140 PRINT"Who can post?":PRINT:PRINT"1) Sysop Only":PRINT"2) Validated users only":PRINT"3) All Users":PRINT"4) Password needed":PRINT:INPUT"Which->";A$:X=VAL(A$):IFX<1ORX>4THEN31400
3150 P(EN)=X:RETURN
3160 PRINT"Who can Read Messages?":PRINT:PRINT"1) Validated users Only":PRINT"2) All Users":PRINT:INPUT"Which->";A$:X=VAL(A$):IFX<1ORX>2THEN3160
3170 R(EN)=X:RETURN
3180 PRINT:INPUT"Enter board masters #";I$:B(EN)=VAL(I$):RETURN
3190 PRINT"Name->";:GOSUB2220:T$(EN)=I$:RETURN
3200 PRINT"Maximum Messages->";:GOSUB2220:M(EN)=VAL(I$):RETURN
3210 PRINT"Slot->";:GOSUB2220:S(EN)=VAL(I$):RETURN
3220 PRINT"Volume->";:GOSUB2220:V(EN)=VAL(I$):RETURN
3230 PRINT"Drive->";:GOSUB2220:D(EN)=VAL(I$):RETURN
3240 IFX=1THENO$="Anonymous"
3250 IFX=2THENO$="Any Handle"
3260 IFX=3THENO$="User Name"
3270 RETURN
3280 PRINT"Messages posted under what name:":PRINT:PRINT"1) Anonymous":PRINT"2) Any Handle":PRINT"3) User Name":PRINT:INPUT"Which->";I$:X=VAL(I$):IFX<1ORX>3THEN3280
3290 T(EN)=X:RETURN
3300 PRINT:INPUT"Password->";PA$(EN):IFLEN(PA$(EN))>7THENPRINT:PRINT"7 Characters Max":GOTO3300
3310 RETURN