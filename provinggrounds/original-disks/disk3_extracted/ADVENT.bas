2088 
9283 ="Y"THENPOKE1014,0:POKE952,240:POKE953,253
10 GOSUB50
20 CALL54915
30 ONERRGOTO8700
40 GOTO8710
50 B$(3)="strike":B$(4)="hit":B$(5)="slam":B$(6)="rag":B$(7)="smite":B$(8)="slash"
60 RETURN
70 GOSUB8300:O$=DL$+"[T/"+STR$(TL)+"][?=Menu]->":GOSUB6000:C$="P"
80 CALL54915
90 IFEN>0THENEN=EN+1
100 IFEN=4THENO$=EL$+"Kaboom!  Well, you killed the Black     Adept and destroyed his army but you    died in the process.  So you haven't    really won.  Sorry!"+EL$:R=30:GOSUB7000:GOTO8390
110 PR=R
120 IFI$<>"B"THEN200
130 IFI$="B"ANDMO$<>"C"THENO$=EL$+"There is Nobody here to Bribe!":GOSUB7000:GOTO70
140 O$=EL$+"Enter Amount of Bribe $":GOSUB6000
150 X=VAL(I$)
160 IFX<0ORX>GDTHENO$=EL$+YO$+EL$:GOSUB7000:GOTO140
170 GD=GD-X:BR=BR-X
180 IFBR>0THENO$=EL$+"Gee thanx.  Maybe now I can buy a piece of gum.  I want more!":GOSUB7000:GOTO70
190 GOTO9370
200 IFI$="Z"THEN3500
210 IFI$="P"THENPOKE1,128:O$=DL$+"What is it concerning(40 Char. Max)?"+DL$:GOSUB6000:VTAB(4):PRINTI$:O$=EL$+"If I am around I'll answer.":GOSUB7000:HOME:POKE1,0:GOTO70
220 IFI$<>"T"THEN260
230 IFMO$<>"C"THENO$=EL$+"There Is Nobody Here!":GOSUB7000:GOTO70
240 IFBR>0THENO$=EL$+"Give me "+STR$(BR)+" Gold and I'll talk.":GOSUB8540:GOSUB7000:GOTO70
250 GOTO9370
260 IFI$<>"J"THEN290
270 IFSH=0THENO$=EL$+"There isn't any shop here!":GOSUB7000:GOTO70
280 J$=STR$(SH):A$=ST$:GOSUB9570:CALL35848"STORES"
290 IFI$="U"THEN3130
300 IFI$="V"THEN3680
310 IFI$="G"THEN2960
320 IFI$="?"THENF$="MENU.ADV":GOSUB2880:GOTO70
330 IFI$="D"THEN3070
340 IFI$<>"O"THEN370
350 O$=EL$+"Leave the Adventure?->":GOSUB6000:IFI$<>"Y"THEN70
360 GOTO8390
370 IFI$="L"THEN8710
380 IFCH$=""ANDI$="K"THENO$=EL$+"There isn't a Chest Here!":GOSUB7000:GOTO70
390 IFI$="K"THEN2780
400 IFI$="N"ORI$="S"ORI$="E"ORI$="W"THEN9180
410 IFI$<>"I"THEN470
420 O$="":Y=0
430 FORX=1TO10
440 IFI$(X)<>"Blank"THENO$=O$+EL$+I$(X):Y=Y+1
450 NEXT
460 O$=O$+DL$+"You have "+STR$(10-Y)+" Spaces Free.":GOSUB7000:GOTO70
470 IFI$="Y"THENGOSUB2200
480 IFI$="C"THEN1620
490 IFI$="H"THENGOSUB8650:GOTO70
500 IFI$<>"F"THENEN=EN-1:GOTO70
510 IFBR$="On"THENBR$="Off":GOTO530
520 BR$="On"
530 O$=EL$+"Brief is now "+BR$:GOSUB7000
540 GOTO70
550 Y=0
560 GOSUB50
570 O$=EL$+"You hear footsteps from a nearby room.  They are coming in your direction....."+EL$:GOSUB7000
580 X=INT(US*RND(1))+1
590 PRINTD$"OPENSTATS,L200"C$(15)
600 PRINTD$"READSTATS,R"X
610 INPUTB$(1),B$(2),A1,A2,A3,A4,A5,A6,A7,A8,A9
620 IFA2<0THENA2=A2-(A2*2)
630 IFA7<0THENA7=A7-(A7*2)
640 S1=-1:S2=-1
650 PRINTD$"CLOSE"
660 IFY=15THENO$=EL$+"Jeez...Must have been your imagination.":GOSUB7000:GOTO70
670 IFB$(2)<>"On"ORB$(1)="EMPTY"ORB$(1)="Empty"ORA=XTHENY=Y+1:GOTO580
680 GOSUB9610
690 O$=EL$+B$(1)+" walks into the room and draws his weapon!"
700 GOSUB8540:GOSUB7000
710 O$=DL$:GOSUB7000
720 IFAG>A2THENO$="You get first attack!":GOSUB7000:GOTO1090
730 O$="He gets first Attack!":GOSUB7000
740 O$=DL$:GOSUB7000:X=INT(3*RND(1))+1:IFX=2THEN1870
750 IFA2>50THEN890
760 G(1)=A3+A2
770 G(2)=INT((WE+AG)*RND(1))+1
780 AM=INT((A7+A2+(2*MP))*RND(1))+1:IFAR>AMTHENO$="Your "+AM$+" Absorbes the Damage!":GOSUB8540:GOSUB7000:GOTO1090
790 IFG(2)<G(1)THEN890
800 X=INT(5*RND(1))+1
810 IFX=1THENO$=B$(1)+" swings at you with his "+MW$+" and misses!"
820 IFX=2THENO$="You duck skillfully under his blow!"
830 IFX=3THENO$="He swings at you but you jump nimbly aside!"
840 IFX=4THENO$=B$(1)+"s "+MW$+" flies over your head just missing you!"
850 IFX=5THENO$="His weapon misses you by a mile!"
860 GOSUB8540
870 GOSUB7000
880 GOTO1090
890 G(3)=(A7/25)+1:G(3)=(G(3)*MP)
900 G(8)=0
910 G(5)=G(3)*(AP*(1/100))
920 G(3)=G(3)-G(5)
930 G(4)=G(3)/4:G(3)=G(3)-(G(4)*RND(1))+G(4)
940 G(3)=INT(G(3))
950 X=INT(5*RND(1))+3
960 IFG(6)>0THENG(6)=G(6)-1:G(3)=INT(G(3)-(G(3)/3))
970 IFG(8)=0THEN990
980 IFMO$="Y"THENO$=B$(1)+" casts a "+S$(G(8))+" spell which "+B$(X)+"s you doing "+STR$(G(3))+" damage.":GOSUB8540:GOSUB7000:GOTO1020
990 O$=B$(1)+" "+B$(X)+"s you with his "+MW$+" doing "+STR$(G(3))+" damage."
1000 GOSUB8540
1010 GOSUB7000
1020 HP=HP-G(3):IFHP>0THEN1090
1030 O$=DL$+B$(1)+" has killed you!"+DL$+"He gets all your gold!"+EL$
1040 GOSUB7000
1050 IFS1<0THENO$=EL$+"Your Lucky!  You don't get logged off   if you are killed by another user.":GOSUB7000:HP=TH:PO=TP:GOTO70
1060 GD=0:IFB$="Y"ANDB$(1)="A Black Adept"THENO$=EL$+"You have been Resurrected!"+EL$+EL$+"You feel your body shit positions..."+EL$+EL$+"[Press Return]":CR=1:GOSUB7000:GOSUB8000:B$="N":HP=TH:PO=TP:R=30:B$(1)="":GOTO10
1070 IFB$="Y"THENO$=EL$+"You have been Resurrected!":GOSUB7000:FI$="On":B$="N":HP=TH:PO=TP:GOTO70
1080 R=30:GOTO8390
1090 O$=DL$+"F)ight C)ast R)un H)ealth S)tats->":GOSUB6000:C$="W"
1100 IFI$="R"THENO$=EL$+"You throw down all your gold to divert  the creature and take off!"+DL$:GD=0:GOSUB7000:R=PN:GOTO8710
1110 IFI$="S"THENGOSUB2200
1120 IFI$="H"THENGOSUB8650:GOTO1090
1130 IFI$="F"THENO$=EL$:GOSUB7000:GOTO1160
1140 IFI$="C"THEN1620
1150 GOTO1090
1160 IFAG>50THEN1300
1170 G(1)=WE+AG
1180 G(2)=INT((A3+A2)*RND(1))+1
1190 AM=INT((ST+AG+(2*HT))*RND(1))+1:IFA4>AMTHENO$="His "+MH$+" Absorbes the Damage!":GOSUB8540:GOSUB7000:GOTO740
1200 IFG(1)>G(2)THEN1300
1210 X=INT(5*RND(1))+1
1220 IFX=1THENO$="You swing at him with your "+WP$+" but he jumps nimbly aside."
1230 IFX=2THENO$="He ducks skillfully under your blow!"
1240 IFX=3THENO$="You lunge at him but he ducks under your blow!"
1250 IFX=4THENO$="Your "+WP$+" misses him by a mile!"
1260 IFX=5THENO$="Your "+WP$+" flies over his head!"
1270 GOSUB8540
1280 GOSUB7000
1290 GOTO740
1300 G(3)=(ST/25)+1:G(3)=(G(3)*HT)
1310 G(5)=G(3)*(MH*(1/100))
1320 G(3)=G(3)-G(5)
1330 G(4)=G(3)/4:G(3)=G(3)-(G(4)*RND(1))+G(4)
1340 G(3)=INT(G(3))
1350 X=INT(6*RND(1))+3
1360 IFI$="C"THENO$="Your "+S$(Y)+" spell "+B$(X)+"s the monster doing "+STR$(G(3))+" damage!":GOSUB8540:GOSUB7000:GOTO1400
1370 O$="You "+B$(X)+" him with your "+WP$+" doing "+STR$(G(3))+" damage!"
1380 GOSUB8540
1390 GOSUB7000
1400 A9=A9-G(3):IFA9>0THEN740
1410 O$=DL$+"You've killed "+B$(1)+"!"+EL$
1420 O$=O$+EL$+"You gain "+STR$(A6)+" gold and "+STR$(A5)+" exp!"+EL$:GOSUB7000
1430 GD=GD+A6:EX=EX+A5:A9=A8:A6=0:B$(2)="Dead"
1440 O$=EL$:GOSUB7000
1450 IFD5<1THEN1470
1460 MF$=MF$+STR$(1000-R)
1470 GOTO70
1480 X=(X*20)+1
1490 X=INT(20*RND(1))+X
1500 GOSUB50
1510 IFD5=-11ANDI$="GUARD"THENRETURN
1520 PRINTD$"OPENMONSTERS,L60"C$(9)
1530 PRINTD$"READMONSTERS,R"X
1540 INPUTB$(1),A7,A2,A8,A3,A4,A6,S1,S2,A9
1550 PRINTD$"CLOSE"
1560 A5=(((A9/TH)*8)/100)*EX:A5=INT(A5)+100
1570 NE$="N"
1580 GOSUB9610
1590 IFI$="Black Adept"THENB$(1)=I$:A9=A9*3
1600 IFI$="GUARD"THENB$(1)="Guard"
1610 O$=EL$+"A vicious "+B$(1)+" attacks you!":GOSUB8540:GOSUB7000:B$(1)="A "+B$(1):GOTO710
1620 IFNE$="Y"THENO$=EL$+"Sorry, Negate has been activated.  All  spells are impotent.":GOSUB7000:GOTO1090
1630 O$=EL$+"Cast(?): ":GOSUB6000
1640 IFI$<>"?"THENO$=I$:GOSUB9620:GOTO1690
1650 O$=EL$+"Name / Quantity / Power Required"+EL$+"--------------------------------"+DL$:GOSUB7000
1660 FORX=1TOSN:IFS(X)>0THENO$=S$(X)+"-"+STR$(S(X))+" ("+STR$(X*2)+")"+EL$:GOSUB7000
1670 NEXT
1680 GOTO1620
1690 FORX=1TOSN:IFS$(X)=B$(0)THEN1720
1700 NEXT
1710 O$=EL$+"There is no such spell!":GOSUB7000:GOTO8680
1720 IFS(X)=0THENO$=EL$+"You don't have that spell!":GOSUB7000:GOTO8680
1730 IFB$(1)="A Black Adept"THENO$=EL$+"Your spells don't seem to work on the   Black Adept!"+EL$:GOSUB7000:GOTO1090
1740 IFC$="P"ANDU(X)<>0THENO$=EL$+"Not a Peace Time spell!":GOSUB7000:GOTO70
1750 IFC$="P"THEN1970
1760 IFS$(X)="Death"ORS$(X)="Teleport"ORS$(X)="Negate"THEN1780
1770 IFU(X)<1THENO$=EL$+"Not a Battle spell!":GOSUB7000:GOTO1090
1780 IFPO<(X*2)THENO$=EL$+"You don't have enough power!":GOSUB7000:GOTO1090
1790 PO=PO-(X*2)
1800 S(X)=S(X)-1
1810 O$=EL$+"Alakazam!"+DL$:GOSUB7000:I$="C"
1820 IFS$(X)="Teleport"THENR=PN:GOTO8710
1830 Y=X
1840 IFS$(X)="Death"THENG(3)=A9/2:GOTO1340
1850 IFS$(X)="Negate"THENNE$="Y":O$="Negate has been activated.  All magical spells are impotent for the duration of this battle.":GOSUB7000:GOTO760
1860 G(3)=(WI/25)+1:G(3)=(G(3)*U(X)):GOTO1310
1870 IFS1<2ORS2<2ORNE$="Y"THEN760
1880 G(8)=0
1890 IFS1<2THENG(8)=S2
1900 IFS2<2THENG(8)=S1
1910 IFG(8)<>0THEN1950
1920 X=INT(2*RND(1))+1
1930 IFX=1THENG(8)=S1
1940 IFX=2THENG(8)=S2
1950 IFS$(G(8))="Death"THENG(3)=HP/2:GOTO940
1960 G(3)=(A8/25)+1:G(3)=(G(3)*U(G(8))):GOTO910
1970 IFPO<(X*2)THENO$=EL$+"You don't have enough power!":GOSUB7000:GOTO70
1980 IFB$="Y"ANDS$(X)="Resurrect"THENO$=EL$+"Resurrect has already been Activated."+EL$:GOSUB7000:GOTO70
1990 PO=PO-(X*2):S(X)=S(X)-1
2000 O$=EL$+"Alakazam!!!"+EL$:GOSUB7000
2010 IFS$(X)="Inviso"THENG(0)=G(0)+15:O$=EL$+"Inviso has been Activated"+DL$+"Estimated lasting time ["+STR$(G(0))+"] Moves":GOSUB7000:GOTO70
2020 IFS$(X)<>"Protect"ANDS$(X)<>"Super Protect"THEN2060
2030 IFS$(X)="Protect"THENG(6)=G(6)+10:O$="Protect "
2040 IFS$(X)="Super Protect"THENG(6)=G(6)+20:O$="Super Protect "
2050 O$=EL$+O$+"has been Activated"+DL$+"Estimated Lasting Time ["+STR$(G(6))+"] Hits":GOSUB7000:GOTO70
2060 IFS$(X)="Resurrect"THENB$="Y":O$=EL$+"Resurrect has been Activated."+EL$:GOSUB7000:GOTO70
2070 IFS$(X)="Super Increase"THENHP=TH*2:PO=TP*2:O$=EL$+"Hit Points = "+STR$(HP)+"!"+DL$+"Power = "+STR$(PO)+"!"+EL$:GOSUB7000:GOTO70
2080 IFS$(X)<>"Increase"THEN2130
2090 O$=EL$+"[H/it points-P/ower]->":GOSUB6000:IFI$<>"H"ANDI$<>"P"THEN2090
2100 IFI$="H"THENHP=TH*2:O$=EL$+"Hit points = "+STR$(HP)
2110 IFI$="P"THENPO=TP*2:O$=EL$+"Power = "+STR$(PO)
2120 O$=O$+"!":GOSUB7000:GOTO70
2130 IFS$(X)="Cure Light Wounds"THENG(3)=INT(10*RND(1))+1:G(4)=INT(10*RND(1))+1
2140 IFS$(X)="Cure Severe Wounds"THENG(3)=INT(20*RND(1))+10:G(4)=INT(20*RND(1))+10
2150 IFS$(X)="Cure All Wounds"THENHP=TH:PO=TP:O$=EL$+"All hit points and power restored!":GOSUB7000:GOTO70
2160 O$=EL$+"Hit points +"+STR$(G(3))+DL$+"Power +"+STR$(G(4)):HP=HP+G(3):PO=PO+G(4):IFHP>THTHENHP=TH
2170 IFPO>TPTHENPO=TP
2180 GOSUB7000
2190 GOTO70
2200 O$=EL$+"Strength: "+STR$(ST)+EL$+"Wisdom: "+STR$(WI)+EL$+"Agility: "+STR$(AG)+EL$+"Intelligence: "+STR$(CH)+EL$+EL$+"Gold: "+STR$(GD)+EL$+"Exp: "+STR$(EX)+EL$+EL$+"Weapon: "+WP$+EL$+"Armor: "+AM$+EL$
2210 O$=O$+EL$+"Protection Left: "+STR$(G(6))+" Hits"
2220 O$=O$+EL$+"Inviso Left: "+STR$(G(0))+" Moves"
2230 O$=O$+EL$+"Ressurect: ":IFB$="Y"THENO$=O$+"On":GOTO2250
2240 O$=O$+"Off"
2250 O$=O$+EL$:GOSUB7000
2260 RETURN
2270 B$(0)="There is a Force Field to the "
2280 IFD1<0THENO$=EL$+B$(0)+"North."+EL$:GOSUB7000
2290 IFD2<0THENO$=EL$+B$(0)+"South."+EL$:GOSUB7000
2300 IFD3<0THENO$=EL$+B$(0)+"West."+EL$:GOSUB7000
2310 IFD4<0THENO$=EL$+B$(0)+"East."+EL$:GOSUB7000
2320 IFLEN(OB$)=0THEN2370
2330 FORX=1TO10
2340 IFI$(X)=OB$OROB$=WP$OROB$=AM$THENO$="":GOTO2370
2350 NEXT
2360 O$=EL$+"There is a "+OB$+" lying on the ground."+EL$:GOSUB8540:GOSUB7000
2370 IFI$="L"ANDCH$="Y"THENO$=EL$+"There is a Chest in the Room."+EL$:GOSUB7000
2380 IFMO$="C"ANDI$="L"THENO$=EL$+"There is a "+TR$+" in the Room."+EL$:GOSUB8540:GOSUB7000
2390 IFI$="L"THEN70
2400 IFD5=-8ANDB$="Y"THENB$(1)="A Black Adept":GOTO1060
2410 IFD5=-8THENO$=EL$+"You Are Dead!"+EL$:GOSUB7000:GOTO1080
2420 IFD5<1ORD5>199THEN2480
2430 IFLEN(MF$)<3THEN2470
2440 FORX=1TOLEN(MF$)STEP3
2450 IFVAL(MID$(MF$,X,3))=1000-RTHENX=11:I$="":GOTO2630
2460 NEXT
2470 IFD5>0THENMO$="Y":X=D5:GOTO1500
2480 CH$="":X=INT(35*RND(1))+1
2490 IFD5=-1THENX=11
2500 IFD5<>-2THEN2560
2510 IFLEN(TF$)<3THEN2550
2520 FORX=1TOLEN(TF$)STEP3
2530 IFVAL(MID$(TF$,X,3))=1000-RTHENX=11:I$="":GOTO2560
2540 NEXT
2550 X=22
2560 IFD5=-3ORD5=-7THENX=29
2570 IFD5=-4THENX=35
2580 IFD5=-5THENX=1
2590 IFD5=-14THENX=L-1:I$="Black Adept":GOSUB1480:D5=X:GOTO2430
2600 IFD5=-11THENX=L-1:I$="GUARD":GOSUB1480:D5=X:GOTO2430
2610 IFD5>199THENMO$="Y":GOTO9370
2620 IF((X>0ANDX<11)OR(X>30ANDX<36))ANDD5<>-5ANDD5<>-4ANDG(0)>0THENX=11
2630 IFX>0ANDX<11THENX=L-1:MO$="Y":GOTO1480
2640 IFX>10ANDX<21THEN70
2650 B(0)=X
2660 X=L*L*10
2670 Y=X/2
2680 K=INT(Y*RND(1))+(X-(X/2))
2690 T=INT(LE*L+1)
2700 IFB(1)=-99THENB(1)=0:RETURN
2710 IFB(0)>20ANDB(0)<26THENCH$="Y":O$=EL$+"There is a Chest In the Room.":GOSUB7000:GOTO70
2720 IFB(0)>30ANDB(0)<36THENMO$="":GOTO550
2730 O$=EL$+"You walk into a Trap!":GOSUB7000
2740 IFD5=-7THEN2770
2750 B(0)=INT(100*RND(1))+1
2760 IFAG>B(0)THENO$=DL$+"You quickly jump aside!":GOSUB7000:GOTO70
2770 B(0)=2:GOTO2840
2780 B(0)=INT(3*RND(1))+1
2790 O$=EL$+"You open the Chest!":CH$="":GOSUB7000
2800 B(1)=-99:GOSUB2660
2810 O$=DL$+"You find "
2820 IFD5=-2THENB(0)=1:TF$=TF$+STR$(1000-R)
2830 IFB(0)=1THENK=K*L*L*5:O$=O$+"Treasure!"+DL$+"Gold + "+STR$(K):GD=GD+K:GOSUB7000:GOTO70
2840 IFB(0)=2THENO$=DL$+"There is an Explosion!"+DL$+"Hit Points -"+STR$(T):HP=HP-T:GOSUB7000:IFHP<1THENHP=0:MO$="Y":IFHP=0THENO$=DL$+"You are Dead!":GOSUB7000:GOTO1070
2850 IFB(0)=2THENX=INT(5*RND(1))+1:IFX=3ORD5=-7THENO$=DL$+"You Are Poisoned!":PO$="Y":GOSUB7000
2860 IFB(0)=2THEN70
2870 O$=O$+"Ash!"+DL$+"Hit Points + "+STR$(T):HP=HP+T:GOSUB7000:GOTO70
2880 PRINTD$"OPEN"F$C$(17)
2890 PRINTD$"READ"F$
2900 &C:IFPEEK(960)=PEEK(971)ANDRT<>3THENPOKE960,0:GOTO2900
2910 IFPEEK(960)=PEEK(970)ANDRT=5THENPOKE960,0:GOTO2900
2920 PRINTCHR$(4)"CLOSE":IFPEEK(960)=255THEN60
2930 IFPEEK(960)=PEEK(970)THENPOKE960,3:O$=EL$:GOTO7000
2940 IFPEEK(960)=PEEK(971)THENPOKE960,2
2950 RETURN
2960 IFLEN(OB$)=0THENO$=EL$+"There is Nothing to Get!":GOSUB7000:GOTO70
2970 IFLEN(OB$)<7THEN3010
2980 FORX=1TOSN
2990 IFS$(X)=LEFT$(OB$,LEN(OB$)-6)THEN3050
3000 NEXT
3010 FORX=1TO10:IFI$(X)="Blank"THEN3040
3020 NEXT
3030 O$=EL$+"You can't Carry any more!":GOSUB7000:GOTO70
3040 O$=EL$+"You Get the "+OB$+".":GOSUB8540:GOSUB7000:I$(X)=OB$:OB$="":AB$="":GOTO70
3050 IFS(X)>8THENO$=EL$+"You can't have more than 9 of any       Spell!":GOSUB7000:GOTO70
3060 S(X)=S(X)+1:O$=EL$+"You pick up the "+OB$+" and quickly learn it.":OB$="":GOSUB8540:GOSUB7000:FS$=FS$+STR$(1000-R):GOTO70
3070 O$=EL$+"What?:":GOSUB6000:O$=I$:GOSUB9620:I$=B$(0)
3080 FORX=1TO10
3090 IFI$(X)=I$THEN3120
3100 NEXT
3110 O$=EL$+"You don't have that item!":GOSUB7000:GOTO70
3120 O$=EL$+"As you drop the "+I$(X)+" it dissapears!":I$(X)="Blank":GOSUB8540:GOSUB7000:GOTO70
3130 O$=EL$+"Use What?->":GOSUB6000:O$=I$:GOSUB9620:I$=B$(0)
3140 FORX=1TO10
3150 IFI$(X)=I$THEN3180
3160 NEXT
3170 O$=EL$+"You Don't have that Item!":GOSUB7000:GOTO70
3180 IFI$="Bottle Of Holy Water"THENO$=EL$+"You open the bottle and drink deeply.   You feel faint, and briefly pass out.   When you awake, the bottle is gone.":HP=TH*3:PO=TP*3:GOSUB7000:I$(X)="Blank":GOTO70
3190 IFR=85ANDI$="Thermonuclear Bomb"THENEN=1
3200 IFI$="Magic Potion"THENO$=EL$+"You open the bottle and drink deply. A  Brief warmth comes over your body.  The bottle then dissapears.":PO$="N":I$(X)="Blank":GOSUB7000:GOTO70
3210 IFI$<>"Box Of Teleportation"THEN3280
3220 O$=EL$+"A) Set Coordinates"+EL$+"B) Teleport"+DL$+"Which(Q=Quit)->":GOSUB6000:IFI$="Q"THEN70
3230 IFI$<>"A"ANDI$<>"B"THEN3220
3240 IFI$="B"ANDTC=0THENO$=EL$+"You have not set the coordinates for    a location yet!":GOSUB7000:GOTO70
3250 IFEN>0ANDI$="B"THENO$=EL$+"Bleep....Oh NO!  Something is affecting the Box!  It won't work!":GOSUB7000:GOTO70
3260 IFI$="B"THENR=TC:O$=EL$+"Poof!"+EL$:GOSUB7000:GOTO8710
3270 IFI$="A"THENTC=R:O$=EL$+"You set the Coordinates for this room.":GOSUB7000:GOTO70
3280 IFI$<>BO$THENO$=EL$+"Nothing Happens.":GOSUB7000:GOTO70
3290 PRINTD$"OPENUSE,L110"C$(29)
3300 PRINTD$"READUSE,R"R
3310 INPUTD6,D7,D8,D9,RR,AB$,B$(0),O$,A$(0)
3320 PRINTD$"CLOSE"
3330 O$=EL$+O$+EL$+A$(0)+EL$:GOSUB7000
3340 IFB$(0)<>"N"THEN3370
3350 FORX=1TO10:IFI$(X)=I$THENI$(X)="Blank"
3360 NEXT
3370 IFLEN(AB$)=0THEN3430
3380 IFRIGHT$(AB$,5)="Spell"ANDLEN(FS$)>0THENMO$="SC":GOSUB8990:MO$="C":IFLEN(AB$)=0THEN3430
3390 FORX=1TO10
3400 IFI$(X)=AB$THEN3440
3410 NEXT
3420 O$=EL$+"A "+AB$+" Appears at your feet.":GOSUB8540:GOSUB7000:OB$=AB$
3430 B$(1)="A Exit Opens up to the "
3440 IFD6<>0THEND1=D6:O$=EL$+B$(1)+"North."+EL$:GOSUB7000
3450 IFD7<>0THEND2=D7:O$=EL$+B$(1)+"South."+EL$:GOSUB7000
3460 IFD8<>0THEND3=D8:O$=EL$+B$(1)+"West."+EL$:GOSUB7000
3470 IFD9<>0THEND4=D9:O$=EL$+B$(1)+"East."+EL$:GOSUB7000
3480 IFRR=RTHEN70
3490 R=RR:O$=EL$:GOSUB7000:GOTO8710
3500 O$=EL$+"Equip with what Item?->":GOSUB6000:O$=I$:GOSUB9620:I$=B$(0)
3510 FORX=1TO10
3520 IFI$=I$(X)THEN3550
3530 NEXT
3540 O$=EL$+"You don't have that Item!":GOSUB7000:GOTO70
3550 Y=X
3560 PRINTD$"OPENWEAPONS,L40"C$(19)
3570 FORX=1TOWN
3580 PRINTD$"READWEAPONS,R"X:INPUTB$(0),T,T:IFB$(0)=I$(Y)THENI$(Y)=WP$:WE=X:HT=T:PRINTD$"CLOSE":GOTO3670
3590 NEXTX
3600 PRINTD$"CLOSE"
3610 PRINTD$"OPENARMOR,L40"C$(1)
3620 FORX=1TOAN
3630 PRINTD$"READARMOR,R"X:INPUTB$(0),T,T:IFB$(0)=I$(Y)THENI$(Y)=AM$:AR=X:AP=T:PRINTD$"CLOSE":GOTO3670
3640 NEXT
3650 PRINTD$"CLOSE"
3660 O$=EL$+"That's not a Weapon or an Armor!":GOSUB7000:GOTO70
3670 O$=EL$+"Equipment transfer complete.":GOSUB7000:GOTO70
3680 O$=EL$+"Say What?->":GOSUB6000:O$=I$:GOSUB9620:I$=B$(0)
3690 IFRIGHT$(N$,3)<>"Inn"THEN3810
3700 X=VAL(I$):IFX<1ORX>5THENO$=EL$+"Sorry, you can't stay longer than 5     hours.":GOSUB7000:GOTO70
3710 IFX>TLTHENO$=EL$+"You don't have that much time left!":GOSUB7000:GOTO70
3720 O$=EL$+"The Manager nods and takes you to your  room.  You quickly fall asleep."+DL$:GOSUB7000
3730 FORY=1TOX:O$="zzzzz....":GOSUB7000:FORT=1TO500:NEXTT:NEXTY
3740 FORY=1TOX
3750 HP=HP+(TH/5):PO=PO+(TP/5)
3760 NEXT
3770 TI=TI-X:IFHP>THTHENHP=TH
3780 IFPO>TPTHENPO=TP
3790 HP=INT(HP):PO=INT(PO)
3800 O$=DL$+"You wake up refreshed and ready to take on the world!":GOSUB7000:GOTO70
3810 IFLEN(BO$)=0ORLEFT$(BO$,1)<>"#"THENO$=EL$+"Nothing Happens.":GOSUB7000:GOTO70
3820 I$="#"+I$
3830 GOTO3280
6000 GOSUB7000:GOSUB8000:RETURN
7000 &B/O$:RETURN
8000 CC=0:IFPEEK(1)=128ANDCF=1ANDCA$="Y"THENCA$="N":CALL38539:CF=9
8010 IFCF=9ANDPEEK(1)<>128THENCF=1:CA$="Y"
8020 IFCA$<>"Y"ORLEN(CN$)>1THEN8040
8030 IFCF=1THENCALL38539
8040 IFPEEK(1)=128THEN8140
8050 IFLEN(CN$)=0THEN8140
8060 FORT=1TOLEN(CN$)
8070 IFMID$(CN$,T,1)=","THENI$=MID$(CN$,1,T-1):CN$=RIGHT$(CN$,LEN(CN$)-T):GOTO8110
8080 IFMID$(CN$,T,1)="#"THENI$=MID$(CN$,1,T-1):CN$="":GOTO8110
8090 NEXT
8100 POKE960,0
8110 IFO$="STOP"THENO$="":GOTO8210
8120 IFLEN(I$)=0THEN8050
8130 O$=I$+EL$:GOSUB7000:GOTO8290
8140 POKE960,0:&A/SP$:I$=MID$(SP$,1):IFPEEK(961)THEN1560
8150 CN$=I$+"#":O$="STOP"
8160 IFLEFT$(I$,1)="#"THENI$="#":CN$=""
8170 POKE961,0:IFPEEK(960)=255THENCC=CC+1:IFCC>1ORI$="OFF"THEN8390
8180 IFPEEK(960)=255THEN8140
8190 IFPEEK(1)=128THENCN$="":GOTO8230
8200 IFLEN(CN$)>0THEN8060
8210 IFLEN(I$)<>1ANDRIGHT$(I$,1)=" "THENI$=LEFT$(I$,LEN(I$)-1):GOTO8210
8220 IFLEN(I$)<>1ANDLEFT$(I$,1)=" "THENI$=RIGHT$(I$,LEN(I$)-1):GOTO8220
8230 IFLEN(I$)=0ANDCR=1THENO$=EL$:GOSUB7000:GOTO8250
8240 IFLEN(I$)=0THEN8000
8250 IFCA$<>"Y"THEN8290
8260 IFCF=1THENCALL36798
8270 IFI$="202 ON"THENO$=EL$+"202 is now On.":GOSUB7000:CALL36798:CF=1
8280 IFI$="202 OFF"THENO$=EL$+"202 is now Off.":GOSUB7000:CALL38539:CF=0
8290 CR=0:RETURN
8300 IFCL$="N"THENT$=DA$:RETURN
8310 PR#4:IN#4:INPUT"#";T,T,T,T,T,B:INPUT"%";T$
8320 IFTI=0THENTI=T
8330 PR#0:POKE56,102:POKE57,213:CALL1002
8340 IFTI>TTHENT=TI+(60-TI)+T
8350 TL=CO-(T-TI)
8360 IFEN>0THEN8380
8370 IFTL<=0THENO$=EL$+"You are out of Time!":GOSUB7000:GOTO8390
8380 RETURN
8390 O$=DL$+"Later Dude!":GOSUB7000:PRINTD$"OPENITEMS"C$(25):PRINTD$"READITEMS"
8400 FORX=1TOIN
8410 INPUTI$
8420 FORY=1TO10
8430 IFI$(Y)=I$THENB(Y)=X
8440 NEXT
8450 NEXT:PRINTD$"CLOSE"
8460 FORX=1TO10:IFI$(X)="Blank"THENB(X)=0
8470 NEXT
8480 IFCP$="N"THENST=ST-(ST*2)
8490 PRINTD$"OPENSAVE,L48"C$(27):PRINTD$"WRITESAVE,R"A:PRINTR:FORX=1TO10:PRINTB(X):NEXT:PRINTTC:PRINTD$"CLOSE":HP=TH:PO=TP:GOSUB8500:PRINTD$"RUNSTART"C$(14)
8500 PRINTD$"OPENSTATS,L200"C$(15):PRINTD$"WRITESTATS,R"A:PRINTNA$:PRINTFI$:PRINTLE:PRINTAG:PRINTWE:PRINTAR:PRINTEX:PRINTGD:PRINTST:PRINTTH:PRINTHP:PRINTPA$:PRINTWI:PRINTCH:PRINTLS:PRINTCA:PRINTCL:PRINTAD$:PRINTCP
8510 PRINTFD:PRINTPO:PRINTTP:PRINTBA:PRINTPA:PRINTDE:PRINTDA$
8520 FORX=1TOSN:PRINTS(X):NEXT
8530 PRINTD$"CLOSE":RETURN
8540 IFLEN(O$)<40THEN8640
8550 Y=40
8560 X=0
8570 IFMID$(O$,Y-X,1)=" "THENB$(0)=LEFT$(O$,(Y-1)-X):B$(0)=B$(0)+EL$+RIGHT$(O$,LEN(O$)-(Y-X)):GOTO8590
8580 X=X+1:GOTO8570
8590 O$=B$(0)
8600 IF(Y+40)>LEN(O$)THEN8640
8610 Y=Y+39
8620 Y=(Y-X)+1
8630 GOTO8560
8640 RETURN
8650 O$=EL$+"Hit Points "+STR$(HP)+" out of "+STR$(TH)+"."+EL$+"Power "+STR$(PO)+" out of "+STR$(TP)+"."+EL$+"Food: "+STR$(FD)+EL$+"Poisoned: ":IFPO$="Y"THENO$=O$+"Yes":GOTO8670
8660 O$=O$+"No"
8670 GOSUB7000:RETURN
8680 IFC$="P"THEN70
8690 GOTO1090
8700 CALL-3288:PRINT"ERROR ON LINE ";:PRINTPEEK(219)*256+PEEK(218):GOTO70
8710 PN=PR:IFI$="L"THEN8730
8720 D6=0:D7=0:D8=0:D9=0:SH=0:CH$="":MO$="":AB$="":OB$="":BR=0:F1=0:DR$="":GI$="":TR$=""
8730 IFR>299THEN9510
8740 IFR<>299THEN8770
8750 IFCF=1THENCALL38539
8760 F$="ADV.SOLVE":GOSUB2880:GOTO9720
8770 PRINTD$"OPENDES,L410"C$(24)
8780 PRINTD$"READDES,R"R
8790 INPUTN$,D1,D2,D3,D4,D5,OB$,BO$,O$
8800 SH=0
8810 IFN$="Adventurers Shop"THENSH=1
8820 IFN$="Hospital"THENSH=2
8830 IFN$="McDonalds"THENSH=3
8840 IFI$="L"ANDD6=0ANDD7=0ANDD8=0ANDD9=0THEN8900
8850 IFI$<>"L"THEN8910
8860 IFD6<>D1ANDD6<>0THEND1=D6
8870 IFD7<>D2ANDD7<>0THEND2=D7
8880 IFD8<>D3ANDD8<>0THEND3=D8
8890 IFD9<>D4ANDD9<>0THEND4=D9
8900 IFLEN(AB$)>0ANDOB$<>AB$THENOB$=AB$
8910 O$=EL$+"["+N$+"]"+"   "+"Exits: "
8920 IFD1<>0THENO$=O$+"N "
8930 IFD2<>0THENO$=O$+"S "
8940 IFD3<>0THENO$=O$+"W "
8950 IFD4<>0THENO$=O$+"E "
8960 O$=O$+EL$:GOSUB7000
8970 IFLEN(OB$)>5ANDRIGHT$(OB$,5)="Spell"ANDLEN(FS$)>0THEN8990
8980 GOTO9030
8990 FORX=1TOLEN(FS$)STEP3
9000 IFVAL(MID$(FS$,X,3))=1000-RTHENOB$="":GI$="":AB$=""
9010 NEXT
9020 IFMO$="SC"THENRETURN
9030 IFR<310ORR>390THEN9060
9040 IFD5=-1THENO$=EL$+"You step into the square and it turns   White.  You are safe.":GOSUB7000
9050 IFD5=-6THENO$=EL$+"You step into the square and it turns   Red.  You are instantly incinerated.":GOSUB7000:GOTO1070
9060 IFD5=-6ORD5=-12ORD5=-13ORD5=-9THEN9090
9070 IFRIGHT$(N$,3)="Inn"THENO$=EL$+"This is a small, quaint little inn      furnished and payed for by the local    government.  There is no charge to      stay."+EL$:GOSUB7000
9080 GOTO9150
9090 O$=EL$:GOSUB7000
9100 IFD5=-6THENO$="You are inside the Red Adepts Castle.":D5=-1
9110 IFD5=-9THENO$="You are inside the White Adepts Castle.":D5=-1
9120 IFD5=-12THENO$="You are inside the Forest of the Elves.":D5=0
9130 IFD5=-13THENO$="You are inside the Black Adepts Castle.":D5=-1
9140 O$=O$+EL$:GOSUB7000
9150 IFBR$="On"ANDI$<>"L"THENPRINTD$"CLOSE":GOTO2270
9160 IFR>299THEN2270
9170 GOSUB2900:GOTO2270
9180 IF(I$="N"ANDD1=0)OR(I$="S"ANDD2=0)OR(I$="W"ANDD3=0)OR(I$="E"ANDD4=0)THENO$=EL$+"You Can't Go That Way.":GOSUB7000:GOTO70
9190 IFI$="N"THENR=D1
9200 IFI$="S"THENR=D2
9210 IFI$="W"THENR=D3
9220 IFI$="E"THENR=D4
9230 IFR>0THEN9310
9240 FORX=1TO10:IFI$(X)="Force Field Pass"THEN9300
9250 NEXT
9260 O$=EL$+"Bzzzzzzzz.  Your body Riddles with      electricity as you pass through the     field.  You need a Force Field Pass to  go through unscathed.":GOSUB7000
9270 X=INT(TH/3):X=INT(X*RND(1))+X
9280 O$=DL$+"You lose "+STR$(X)+" Hit Points.":HP=HP-X:GOSUB7000
9290 O$=EL$:GOSUB7000
9300 R=R-(2*R)
9310 IFPO$="Y"THENO$=EL$+"You're Poisoned!"+DL$+"Hit Points -":T=INT(((LE*L)/2)+1):O$=O$+STR$(T)+EL$:GOSUB7000:HP=HP-T
9320 IFG(0)>0THENG(0)=G(0)-1
9330 FD=FD-3:IFFD<1THENFD=0:O$=EL$+"You're starving!"+DL$+"Hit Points -":T=INT(((LE*L)/2)+1):O$=O$+STR$(T)+EL$:GOSUB7000:HP=HP-T
9340 IFFD>0ANDFD<10THENO$=EL$+"Warning!  You are low on food!"+EL$:GOSUB7000
9350 IFHP<1THENO$=DL$+"You are Dead!"+EL$:GOSUB7000:R=1:GOTO1070
9360 GOTO8710
9370 PRINTD$"OPENCHAR,L200"C$(23)
9380 PRINTD$"READCHAR,R"D5
9390 INPUTTR$,BR,GI$,I$
9400 IFMO$<>"C"THENMO$="C":PRINTD$"CLOSE":GOTO9420
9410 IFMO$="C"THENO$=EL$+"The "+TR$+" Speaks:"+EL$:GOSUB7000:GOSUB2900:GOTO9440
9420 O$=EL$+"You see a "+TR$+".":GOSUB8540:GOSUB7000
9430 GOTO70
9440 IFLEN(GI$)=0THEN70
9450 FORX=1TO10
9460 IFI$(X)=GI$THEN70
9470 NEXT
9480 IFRIGHT$(GI$,5)="Spell"ANDLEN(FS$)>0THENMO$="SC":GOSUB8990:MO$="C":IFLEN(GI$)=0THEN70
9490 O$=EL$+"The "+TR$+" drops a "+GI$+" at your Feet.":GOSUB8540:GOSUB7000:OB$=GI$:AB$=GI$
9500 GOTO70
9510 PRINTD$"OPENSHD,L55"C$(28)
9520 PRINTD$"READSHD,R"R
9530 INPUTN$,D1,D2,D3,D4,D5,OB$
9540 PRINTD$"CLOSE"
9550 IFLEFT$(OB$,1)="*"THENBO$=RIGHT$(OB$,LEN(OB$)-1):OB$=""
9560 GOTO8800
9570 Y=VAL(MID$(A$,4,1)):POKE43626,Y:Y=VAL(RIGHT$(A$,1)):POKE43624,Y:Y=VAL(LEFT$(A$,3)):IFYR$="Y"THENPRINTD$"OPEN VC,V"Y
9575 IFTC$="Y"THENPOKE1014,10:POKE952,215:POKE953,251
9580 RETURN
9590 PRINTD$"OPENWEAPONS,L40"C$(19):PRINTD$"READWEAPONS,R"T:INPUTB$(0),B(3),B(4):T=B(3)/4:PRINTD$"CLOSE":RETURN
9600 PRINTD$"OPENARMOR,L40"C$(1):PRINTD$"READARMOR,R"T:INPUTB$(0),B(3),B(4):PRINTD$"CLOSE":RETURN
9610 T=A3:GOSUB9590:B(2)=T:MP=B(4):MW$=B$(0):T=A4:GOSUB9600:MH=B(4):MH$=B$(0):RETURN
9620 B$(0)="":J=0
9630 FORX=1TOLEN(O$)
9640 A$=MID$(O$,X,1)
9650 IFX=1THENT=ASC(A$):IFT>64ANDT<91THEN9690
9660 T=ASC(A$):IFT<65ORT>90THENJ=1:GOTO9690
9670 IFJ=1THENJ=0:GOTO9690
9680 T=T+32:A$=CHR$(T)
9690 B$(0)=B$(0)+A$
9700 NEXT
9710 RETURN
9720 X=WE+1:IFX>WNTHENX=WN
9730 Y=AR+1:IFY>ANTHENY=WN
9740 WE=X:AR=X
9760 T=WE:GOSUB9590:O$=EL$+"You get a "+B$(0)+"!":T=AR:GOSUB9600:O$=O$+DL$+"You get a "+B$(0)+"!"+EL$:GOSUB7000:CA=0:FORX=1TO10:I$(X)="Blank":NEXT:R=0:GOTO8390